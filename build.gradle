/*
 * This file is part of ProjectX.
 * Copyright (c) 2015 - 2018, KitsuneAlex, All rights reserved.
 *
 * ProjectX is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * ProjectX is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with ProjectX.  If not, see <http://www.gnu.org/licenses/lgpl>.
 */

buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.12.2-2.2.1"
group = "de.kitsunealex.projectx"
archivesBaseName = "ProjectX"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.3.2655"
    runDir = "run"
    mappings = "snapshot_20171003"
}

repositories {
	maven { url = "http://chickenbones.net/maven" }
	maven { url = "http://dvs1.progwml6.com/files/maven" }
	maven { url = "http://tehnut.info/maven" }
	maven { url = "http://maven.tterrag.com" }
	maven { url = "http://maven.covers1624.net" }
}

dependencies {
	deobfCompile "codechicken:CodeChickenLib:1.12.2-3.1.7.340:deobf"
	deobfCompile "mezz.jei:jei_1.12.2:4.9.1.169:api"
	deobfCompile "mcp.mobius.waila:Hwyla:1.8.23-B38_1.12:api"
	deobfCompile "cofh:RedstoneFlux:1.12-2.0.1.2:deobf"
	runtime "mezz.jei:jei_1.12.2:4.9.1.169"
    runtime "mcp.mobius.waila:Hwyla:1.8.23-B38_1.12"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    classifier = 'universal'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives deobfJar
    archives srcJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (project.hasProperty("local_maven")) {
                repository(url: "file://${local_maven}")
                pom {
                    groupId = project.group
                    artifactId = project.archivesBaseName
                    project {
                        name project.archivesBaseName
                        packaging 'jar'
                        description 'ProjectX'
                        url 'https://github.com/KitsuneAlex/ProjectX'

                        scm {
                            url 'https://github.com/KitsuneAlex/ProjectX'
                            connection 'scm:git:git://github.com/KitsuneAlex/ProjectX.git'
                            developerConnection 'scm:git:git@github.com:KitsuneAlex/ProjectX.git'
                        }

                        issueManagement {
                            system 'github'
                            url 'https://github.com/KitsuneAlex/ProjectX/issues'
                        }

                        licenses {
                            license {
                                name 'GNU Lesser Public License (GPL), Version 2.1'
                                url 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id 'KitsuneAlex'
                                name 'KitsuneAlex'
                                roles { role 'developer' }
                            }
                        }
                    }
                }
            }
        }
    }
}